5 RANDOMIZE TIMER
10 REM SUPER STARTREK - MAY 16,1978 - REQUIRES 24K MEMORY (AT LEAST)
20 REM
30 REM ****        **** STAR TREK ****      ****
40 REM **** SIMULATION OF A MISSION OF THE STARSHIP ENTERPRISE,
50 REM **** AS SEEN ON THE STAR TREK TV SHOW.
60 REM **** ORIGINAL PROGRAM BY MIKE MAYFIELD, MODIFIED VERSION
70 REM **** PUBLISHED IN DEC'S "101 BASIC GAMES", BY DAVE AHL.
80 REM **** MODIFICATIONS TO THE LATTER (PLUS DEBUGGING) BY BOB
90 REM *** LEEDOM - APRIL & DECEMBER 1974,
100 REM *** WITH A LITTLE HELP FROM HIS FRIENDS . . .
110 REM *** COMMENTS, EPHITETS, AND SUGGESTIONS SOLICITED --
120 REM *** SEND TO: R.C. LEEDOM
130 REM ***          WESTINGHOSE DEFENSE & ELECTRONICS SYSTEMS CNIR
140 REM ***          BOX 746, M.S. 338
150 REM ***          BALTIMORE, MD 21203
160 REM ***
170 REM *** CONVERTED TO MICROSOFT 8 K BASIC 3/16/78 BY JOHN BORDERS
180 REM *** LINE NUMBERS FROM VERSION TREK7 OF 1/12/75 PRESERVED AS
190 REM *** MUCH AS POSSIBLE WHILE USING MULTIPLE STATEMENTS PER LINE
200 REM ***
210 REM *** MODIFIED TO RUN ON GRANT SEARLE'S 9-CHIP Z80 COMPUTER
220 REM *** AND DERIVATIVES 04-AUG-2018 BY N.KENDRICK
230 REM *** (LINKER3000-AT-GMAIL.COM)
240 REM ***
250 REM *** ADAPTED TO MSBASIC V5.0+ INTEGER ROUNDING ON 18/8/18 BY FEILIPU
260 REM ***
    REM ***  Adapted for the PicoCalc by Ron Lauzon, 3/29/2025
290 CLS: font 7: color rgb(lightgray)
300 PRINT "THE USS ENTERPRISE --- NCC-1701"
310 PRINT
330 PRINT "                  ,------*------,"
340 PRINT "  ,-------------   '---  ------'"
350 PRINT "   '-------- --'      / /"
360 PRINT "       ,---' '-------/ /--,"
370 PRINT "        '----------------'"
410 Z9$="                         "
420 DIM G(8,8)
    DIM C(9,2)
	DIM K(3,3)
	DIM N1(3)
	DIM Z(8,8)
	DIM D(8)
430 T=INT(RND*20+20)*100
    T0=T
	T9=25+INT(RND*10)
	D0=0
	E=3000
	E0=E
440 P=10
    P0=P
	S9=200
	S=0
	B9=0
	K9=0
	x4$=""
450 function fnd(d)
	   fnd=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
	end function
460 function FNR(R)
       fnr=INT(RND*R*7.98+1.01)
	end function
470 Q1=FNR(1)
    Q2=FNR(1)
	S1=FNR(1)
	S2=FNR(1)
480 FOR I=1 TO 9
    C(I,1)=0
	C(I,2)=0
	NEXT I
490 C(3,1)=-1
    C(2,1)=-1
	C(4,1)=-1
	C(4,2)=-1
	C(5,2)=-1
	C(6,2)=-1
500 C(1,2)=1
    C(2,2)=1
	C(6,1)=1
	C(7,1)=1
	C(8,1)=1
	C(8,2)=1
	C(9,2)=1
510 FOR I=1 TO 8
    D(I)=0
	NEXT I
520 A1$="NAVSRSLRSPHATORSHEDAMCOMXXXSTA"
530 FOR I=1 TO 8
    FOR J=1 TO 8
	K3=0
	Z(I,J)=0
	R1=RND
540 IF R1>.98 THEN K3=3:K9=K9+3:GOTO 570
550 IF R1>.95 THEN K3=2:K9=K9+2:GOTO 570
560 IF R1>.8 THEN K3=1:K9=K9+1
570 B3=0
    IF RND>.96 THEN B3=1:B9=B9+1
580 G(I,J)=K3*100+B3*10+FNR(1)
    NEXT J
	NEXT I
	IF K9>T9 THEN T9=K9+1
590 IF B9<>0 THEN goto 620
600 IF G(Q1,Q2)<200 THEN G(Q1,Q2)=G(Q1,Q2)+100:K9=K9+1
610 B9=1
    G(Q1,Q2)=G(Q1,Q2)+10
	Q1=FNR(1)
	Q2=FNR(1)
620 K7=K9
	print
630 PRINT"YOUR ORDERS ARE AS FOLLOWS:"
640 PRINT "--------------------------"
650 PRINT"DESTROY THE";K9;" KLINGON WARSHIPS WHICH HAVE INVADED"
660 PRINT"THE GALAXY BEFORE THEY CAN ATTACK FEDERATION"
670 PRINT"HEADQUARTERS ON STARDATE";T0+T9;". THIS GIVES YOU";T9
680 PRINT"DAYS. THERE'S ";B9;" STARBASES"
700 PRINT"IN THE GALAXY FOR RESUPPLYING YOUR SHIP."
710 PRINT:
    PRINT "PRESS Y TO ACCEPT COMMAND";
720 INPUT I5$:print
730 IF LEFT$(I5$,1)="Y" OR LEFT$(I5$,1)="y" THEN goto 760 
740 GOTO 710
760 Z4=Q1
    Z5=Q2
	K3=0
	B3=0
	S3=0
	G5=0
	D4=.5*RND
	Z(Q1,Q2)=G(Q1,Q2)
770 IF Q1<1 OR Q1>8 OR Q2<1 OR Q2>8 THEN goto 890
780 GOSUB 4250
    PRINT
	IF T0<>T THEN goto 810
790 PRINT"YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
800 PRINT"IN THE GALACTIC QUADRANT, '";G2$;"'.":GOTO 820
810 PRINT"NOW ENTERING ";G2$;" QUADRANT . . ."
820 PRINT
    K3=INT(G(Q1,Q2)*.01)
	B3=INT(G(Q1,Q2)*.1)-10*K3
830 S3=G(Q1,Q2)-100*K3-10*B3
    IF K3=0 THEN goto 880
840 PRINT TAB(3);
    color rgb(black), rgb(red)
    print "  COMBAT AREA      CONDITION RED";
    color rgb(lightgray), rgb(black)
	PRINT
850 IF S>200 THEN PRINT:GOTO 880
860 PRINT TAB(3);
    color rgb(black), rgb(red)
    print "      SHIELDS DANGEROUSLY LOW   ";
    color rgb(lightgray), rgb(black)
	PRINT
870 PRINT
880 FOR I=1 TO 3
    K(I,1)=0
	K(I,2)=0
	NEXT I
890 FOR I=1 TO 3
    K(I,3)=0
	NEXT I
	Q$=Z9$+Z9$+Z9$+Z9$+Z9$+Z9$+Z9$+LEFT$(Z9$,17)
900 a2$="<E>"
    Z1=S1
	Z2=S2
	GOSUB 4080
	IF K3<1 THEN goto 930
910 FOR I=1 TO K3
    GOSUB 4050
	a2$="+K+"
	Z1=R1
	Z2=R2
920 GOSUB 4080
    K(I,1)=R1
	K(I,2)=R2
	K(I,3)=S9*(.5+RND)
	NEXT I
930 IF B3<1 THEN goto 950
940 GOSUB 4050
    a2$=">B<"
	Z1=R1
	B4=R1
	Z2=R2
	B5=R2
	GOSUB 4080
950 FOR I=1 TO S3
    GOSUB 4050
	a2$=" * "
	Z1=R1
	Z2=R2
	GOSUB 4080
	NEXT I
960 GOSUB 3020
970 IF S+E>10 THEN IF E>10 OR D(7)=0 THEN goto 1030
980 PRINT
    PRINT TAB(10);
	color rgb(black), rgb(red)
    print 	"** FATAL ERROR **";
    color rgb(lightgray), rgb(black)
	PRINT
990 PRINT"YOU'VE JUST STRANDED YOUR SHIP IN SPACE."
1000 PRINT"YOU HAVE INSUFFICIENT MANEUVERING ENERGY,"
1010 PRINT"AND SHIELD CONTROL IS PRESENTLY INCAPABLE OF"
1020 PRINT"CROSS-CIRCUITING TO ENGINE ROOM!!"
     PRINT
	 GOTO 2890
1030 PRINT
     INPUT"COMMAND";a2$:PRINT:a2$=UCASE$(a2$)
1040 FOR I=1 TO 10
     IF LEFT$(a2$,3)<>MID$(A1$,3*I-2,3) THEN goto 1060
1050 ON I GOTO 1170,960,1840,1930,2150,2500,2590,3360,2920,3180
1060 NEXT I
     PRINT"ENTER ONE OF THE FOLLOWING:"
1070 PRINT "--------------------------"
1080 PRINT"  NAV  (TO SET COURSE)"
1090 PRINT"  SRS  (FOR SHORT RANGE SENSOR SCAN)"
     PRINT"  STA  (FOR STATUS)"
1100 PRINT"  LRS  (FOR LONG RANGE SENSOR SCAN)"
1110 PRINT"  PHA  (TO FIRE PHASERS)"
1120 PRINT"  TOR  (TO FIRE PHOTON TORPEDOES)"
1130 PRINT"  SHE  (TO RAISE OR LOWER SHIELDS)"
1140 PRINT"  DAM  (FOR DAMAGE CONTROL REPORTS)"
1150 PRINT"  COM  (TO CALL ON LIBRARY-COMPUTER)"
1160 PRINT"  XXX  (TO RESIGN YOUR COMMAND)"
     PRINT
	 GOTO 970
1170 INPUT"COURSE (1-9)";C1:print
     IF C1>=8.5 THEN C1=C1-8
1180 IF C1>=.5 AND C1<8.5 THEN goto 1200
1190 PRINT"   LT. SULU: 'INCORRECT COURSE DATA, SIR!'"
     GOTO 970
1200 x4$="8"
     IF D(1)<0 THEN x4$="0.9"
1210 print "WARP FACTOR (0-";x4$;")";
     input W1:PRINT
1220 IF D(1)<0 AND W1>.9 THEN goto 1270
1230 IF W1>0 AND W1<=8 THEN goto 1280
1240 IF W1=0 THEN goto 970
1250 PRINT"   CHIEF ENGINEER SCOTT: 'THE ENGINES WON'T TAKE";
1260 PRINT" WARP";W1;"!'"
     GOTO 970
1270 color rgb(orange)
     PRINT "WARP ENGINES ARE DAMAGED.  MAXIUM SPEED = WARP 0.9"
     color rgb(lightgray)
     GOTO 970
1280 N=INT(W1*8+.5)
     IF E-N>=0 THEN goto 1350
1290 PRINT"ENGINEERING:  'INSUFFICIENT ENERGY AVAILABLE"
1300 PRINT"               FOR MANEUVERING AT WARP";W1;"!'"
1310 IF S<N-E OR D(7)<0 THEN goto 970
1320 PRINT"DEFLECTOR CONTROL ROOM:  ";S;" UNITS OF ENERGY"
1330 PRINT"                          PRESENTLY DEPLOYED TO SHIELDS."
1340 GOTO 970
1350 FOR I=1 TO K3
     IF K(I,3)=0 THEN goto 1380
1360 a2$="   "
     Z1=K(I,1)
	 Z2=K(I,2)
	 GOSUB 4080
	 GOSUB 4050
1370 K(I,1)=Z1
     K(I,2)=Z2
	 a2$="+K+"
	 GOSUB 4080
1380 NEXT I
     GOSUB 2750
	 D1=0
	 D6=W1
	 IF W1>=1 THEN D6=1
1390 FOR I=1 TO 8
     IF D(I)>=0 THEN goto 1440
1400 D(I)=D(I)+D6
     IF D(I)>-.1 AND D(I)<0 THEN D(I)=-.1:GOTO 1440
1410 IF D(I)<0 THEN goto 1440
1420 IF D1<>1 THEN D1=1:PRINT"DAMAGE CONTROL REPORT:"
1430 PRINT TAB(8);
     R1=I
	 GOSUB 4130
     color rgb(orange)
	 PRINT G2$;" REPAIR COMPLETED."
     color rgb(lightgray)
1440 NEXT I
     IF RND>.2 THEN goto 1500
1450 R1=FNR(1)
     IF RND>=.6 THEN goto 1480
1460 D(R1)=D(R1)-(RND*5+1)
     PRINT"DAMAGE CONTROL REPORT:"
1470 GOSUB 4130
     color rgb(orange)
     PRINT G2$;" DAMAGED"
     color rgb(lightgray)
	 PRINT
	 GOTO 1500
1480 D(R1)=D(R1)+RND*3+1
     PRINT "DAMAGE CONTROL REPORT:"
1490 GOSUB 4130
     PRINT G2$;" STATE OF REPAIR IMPROVED"
	 PRINT
1500 a2$="   "
     Z1=INT(S1)
     Z2=INT(S2)
	 GOSUB 4080
1510 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1))
     X=S1
	 Y=S2
1520 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1))
     Q4=Q1
	 Q5=Q2
1530 FOR I=1 TO N
     S1=S1+X1:S2=S2+X2
1540 IF S1<1 OR S1>=9 OR S2<1 OR S2>=9 THEN goto 1630
1550 S8=INT(S1)*24+INT(S2)*3-26
     IF MID$(Q$,S8,2)="  "THEN goto 1580
1560 S1=INT(S1-X1)
     S2=INT(S2-X2)
	 PRINT"WARP ENGINES SHUT DOWN AT ";
1570 PRINT"SECTOR";S1;",";S2;" DUE TO BAD NAVIGATION"
     GOTO 1590
1580 NEXT I
     S1=INT(S1)
	 S2=INT(S2)
1590 a2$="<E>"
     Z1=INT(S1)
	 Z2=INT(S2)
	 GOSUB 4080
	 GOSUB 1800
	 T8=1
1600 IF W1<1 THEN T8=.1*INT(10*W1)
1610 T=T+T8
     IF T>T0+T9 THEN goto 2890
1620 GOTO 960
1630 X=8*Q1+X+N*X1
     Y=8*Q2+Y+N*X2
	 Q1=INT(X/8)
	 Q2=INT(Y/8)
	 S1=INT(X-Q1*8)
1640 S2=INT(Y-Q2*8)
     IF S1=0 THEN Q1=Q1-1:S1=8
1650 IF S2=0 THEN Q2=Q2-1:S2=8
1660 X5=0
     IF Q1<1 THEN X5=1:Q1=1:S1=1
1670 IF Q1>8 THEN X5=1:Q1=8:S1=8
1680 IF Q2<1 THEN X5=1:Q2=1:S2=1
1690 IF Q2>8 THEN X5=1:Q2=8:S2=8
1700 IF X5=0 THEN goto 1780
1710 PRINT"LT. UHURA: MESSAGE FROM STARFLEET COMMAND --"
1720 PRINT"  'PERMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMETER"
1730 PRINT"  IS HEREBY *DENIED*.  SHUT DOWN YOUR ENGINES.'"
1740 PRINT"CHIEF ENGINEER SCOTT:  'WARP ENGINES SHUT DOWN"
1750 PRINT"  AT SECTOR";S1;",";S2;" OF QUADRANT";
1760 PRINT Q1;",";Q2;".'"
1770 IF T>T0+T9 THEN goto 2890
1780 IF 8*Q1+Q2=8*Q4+Q5 THEN goto 1590
1790 T=T+1
     GOSUB 1800
	 GOTO 760
1800 E=E-N-10
     IF E>=0 THEN RETURN
1810 PRINT "SHIELD CONTROL SUPPLIES ENERGY TO COMPLETE THE MANEUVER."
1820 S=S+E
     E=0
	 IF S<=0 THEN S=0
1830 RETURN
1840 IF D(3)<0 THEN PRINT"LONG RANGE SENSORS ARE INOPERABLE.":GOTO 970
1850 PRINT"LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
     PRINT
1860 O1$="-------------------"
     PRINT O1$
1870 FOR I=Q1-1 TO Q1+1
     N1(1)=-1
	 N1(2)=-2
	 N1(3)=-3
	 FOR J=Q2-1 TO Q2+1
1880 IF I>0 AND I<9 AND J>0 AND J<9 THEN N1(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
1890 NEXT J
     FOR L=1 TO 3
	 PRINT"| ";
1900 IF N1(L)<0 THEN PRINT"*** ";:GOTO 1920
1910 PRINT RIGHT$(STR$(N1(L)+1000),3);" ";
1920 NEXT L
     PRINT"|"
	 PRINT O1$
	 NEXT I
	 GOTO 970
1930 IF D(4)<0 THEN PRINT"PHASERS INOPERATIVE.":GOTO 970
1940 IF K3>0 THEN goto 1970
1950 PRINT"SCIENCE OFFICER SPOCK:  'SENSORS SHOW NO ENEMY SHIPS"
1960 PRINT"                         IN THIS QUADRANT'":GOTO 970
1970 IF D(8)<0 THEN PRINT"COMPUTER FAILURE HAMPERS ACCURACY."
1980 PRINT"PHASERS LOCKED ON TARGET."
1990 PRINT"ENERGY AVAILABLE =";E;" UNITS"
2000 INPUT"NUMBER OF UNITS TO FIRE";X:print
     IF X<=0 THEN goto 970
2010 IF E-X<0 THEN goto 1990
2020 E=E-X
     IF D(7)<0 THEN X=X*RND
2030 H1=INT(X/K3)
     FOR I=1 TO 3
	 IF K(I,3)<=0 THEN goto 2140
2040 H=INT((H1/FND(0))*(RND+2))
     IF H>.15*K(I,3)THEN goto 2070
2050 PRINT"SENSORS SHOW NO DAMAGE TO ENEMY AT";K(I,1);
2060 PRINT",";K(I,2);"."
     GOTO 2140
2070 K(I,3)=K(I,3)-H
     PRINT H;" UNIT HIT ON KLINGON AT SECTOR";
2080 PRINT K(I,1);",";
2090 PRINT K(I,2);"."
     IF K(I,3)<=0 THEN PRINT
2100 color rgb(yellow):
     PRINT "*** KLINGON DESTROYED ***";
	 color rgb(lightgray)
	 PRINT
	 GOTO 2120
2110 PRINT" (SENSORS SHOW";K(I,3);" UNITS REMAINING)"
     GOTO 2140
2120 K3=K3-1
     K9=K9-1
	 Z1=K(I,1)
	 Z2=K(I,2)
	 a2$="   "
	 GOSUB 4080
2130 K(I,3)=0
     G(Q1,Q2)=G(Q1,Q2)-100
	 Z(Q1,Q2)=G(Q1,Q2)
	 IF K9<=0 THEN goto 2990
2140 NEXT I
     GOSUB 2750
	 GOTO 970
2150 IF P<=0 THEN PRINT"ALL PHOTON TORPEDOES EXPENDED.":GOTO 970
2160 IF D(5)<0 THEN PRINT"PHOTON TUBES ARE NOT OPERATIONAL.":GOTO 970
2170 INPUT"PHOTON TORPEDO COURSE (1-9)";C1:print
     IF C1>=8.5 THEN C1=C1-8
2180 IF C1>=.5 AND C1<8.5 THEN goto 2210
2190 PRINT"ENSIGN CHEKOV:  'INCORRECT COURSE DATA, SIR!'"
2200 GOTO 970
2210 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1))
     E=E-2
	 P=P-1
2220 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1))
     X=S1
	 Y=S2
2230 PRINT"TORPEDO TRACK:"
2240 X=X+X1
     Y=Y+X2
	 X3=INT(X+.5)
	 Y3=INT(Y+.5)
2250 IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN goto 2490
2260 PRINT"               ";X3;",";Y3
     a2$="   "
	 Z1=X
	 Z2=Y
2270 GOSUB 4220
2280 IF Z3<>0 THEN goto 2240
2290 a2$="+K+"
     Z1=X
	 Z2=Y
	 GOSUB 4220
	 IF Z3=0 THEN goto 2350
2300 PRINT
     color rgb(yellow)
	 PRINT "*** KLINGON DESTROYED ***";
	 color rgb(lightgray)
	 PRINT
2310 PRINT
     K3=K3-1
	 K9=K9-1
	 IF K9<=0 THEN goto 2990
2320 FOR I=1 TO 3
     IF X3=K(I,1)AND Y3=K(I,2)THEN goto 2340
2330 NEXT I:
     I=3
2340 K(I,3)=0
     GOTO 2470
2350 a2$=" * "
     Z1=X
	 Z2=Y
	 GOSUB 4220
	 IF Z3=0 THEN goto 2380
2360 PRINT"STAR AT";X3;",";Y3;" ABSORBED TORPEDO ENERGY."
     GOSUB 2750
2370 GOTO 970
2380 a2$=">!<"
     Z1=X
	 Z2=Y
	 GOSUB 4220
	 IF Z3=0 THEN goto 2170
2390 color rgb(red):
     PRINT "*** STARBASE DESTROYED ***";
	 color rgb(lightgray)
	 PRINT
2400 B3=B3-1
     B9=B9-1
2410 IF B9>0 OR K9>T-T0-T9 THEN goto 2450
2420 PRINT"THAT DOES IT, CAPTAIN!!  YOU ARE HEREBY RELIEVED OF COMMAND"
2430 PRINT"AND SENTENCED TO 99 STARDATES AT HARD LABOR ON CYGNUS 12!!"
2440 GOTO 2920
2450 PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD TO CONSIDER"
2460 PRINT"COURT MARTIAL!"
     D0=0
2470 Z1=X
     Z2=Y
	 a2$="   "
	 GOSUB 4080
2480 G(Q1,Q2)=K3*100+B3*10+S3
     Z(Q1,Q2)=G(Q1,Q2)
	 GOSUB 2750
	 GOTO 970
2490 PRINT"TORPEDO MISSED."
     PRINT
	 GOSUB 2750
	 GOTO 970
2500 IF D(7)<0 THEN PRINT"SHIELD CONTROL INOPERABLE.":GOTO 970
2510 PRINT"ENERGY AVAILABLE =";E+S
     INPUT"NUMBER OF UNITS TO SHIELDS";X:print
2520 IF X<0 OR S=X THEN PRINT"<SHIELDS UNCHANGED>":GOTO 970
2530 IF X<=E+S THEN goto 2560
2540 PRINT"SHIELD CONTROL:  'THIS IS NOT THE FEDERATION TREASURY.'" 
2550 PRINT"<SHIELDS UNCHANGED>"
     GOTO 970
2560 E=E+S-X
     S=X
	 PRINT"DEFLECTOR CONTROL ROOM:"
2570 PRINT"  'SHIELDS NOW AT";INT(S);" UNITS PER YOUR COMMAND.'"
2580 GOTO 970
2590 IF D(6)>=0 THEN goto 2700
2600 PRINT"DAMAGE CONTROL REPORT NOT AVAILABLE."
     IF D0=0 THEN goto 970
2610 D3=0
     FOR I=1 TO 8
	 IF D(I)<0 THEN D3=D3+.1
2620 NEXT I
     IF D3=0 THEN goto 970
2630 PRINT
     D3=D3+D4
	 IF D3>=1 THEN D3=.9
2640 PRINT"TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR"
2650 PRINT"SHIP. ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);" STARDATES."
2660 INPUT"WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)";a2$:print:a2$=ucase$(a2$)
2670 IF a2$<>"Y"THEN goto 970
2680 FOR I=1 TO 8
     IF D(I)<0 THEN D(I)=0
2690 NEXT I
     T=T+D3+.1
2700 PRINT
     PRINT"DEVICE             STATE OF REPAIR"
2710 PRINT"------             ---------------"
     FOR R1=1 TO 8
2720 GOSUB 4130
     PRINT G2$;LEFT$(Z9$,25-LEN(G2$));INT(D(R1)*100)*.01
2730 NEXT R1
     PRINT
	 IF D0<>0 THEN goto 2610
2740 GOTO 970
2750 IF K3<=0 THEN RETURN
2760 IF D0<>0 THEN PRINT"STARBASE SHIELDS PROTECT THE ENTERPRISE."
2770 RETURN
2780 FOR I=1 TO 3
     IF K(I,3)<=0 THEN goto 2880
2790 H=INT((K(I,3)/FND(1))*(2+RND))
2800 S=S-H
     K(I,3)=K(I,3)/(3+RND(0))
2810 PRINT
     PRINT H;"UNIT HIT ON ENTERPRISE FROM SECTOR";
2820 PRINT K(I,1);",";K(I,2);"."
2830 IF S<=0 THEN goto 2900
2840 PRINT"      <SHIELDS DOWN TO";S;"UNITS>"
     IF H<20 THEN goto 2880
2850 IF RND>.6 OR H/S<=.02 THEN goto 2880
2860 R1=FNR(1):D(R1)=D(R1)-H/S-.5*RND:GOSUB 4130
2870 PRINT"DAMAGE CONTROL: '";G2$;" DAMAGED BY THE HIT'"
2880 NEXT I
     RETURN
2890 PRINT:PRINT"IT IS STARDATE";T;"."
     PRINT
	 GOTO 2920
2900 PRINT:PRINT"THE ENTERPRISE HAS BEEN DESTROYED.  THE FEDERATION ";
2910 PRINT"WILL BE CONQUERED."
     GOTO 2890
2920 PRINT"THERE WERE";K9;" KLINGON BATTLE CRUISERS LEFT AT"
2930 PRINT"THE END OF YOUR MISSION."
2940 PRINT
     PRINT
	 IF B9=0 THEN goto 2980
2950 PRINT"THE FEDERATION IS IN NEED OF A NEW STARSHIP COMMANDER"
2960 PRINT"FOR A SIMILAR MISSION -- IF THERE IS A VOLUNTEER,"
2970 INPUT"LET HIM STEP FORWARD AND ENTER 'AYE'";a2$:print
     IF a2$="AYE" THEN goto 10
2980 PRINT
     PRINT "BACK TO SYSTEM."
	 END
2990 PRINT"CONGRATULATIONS, CAPTAIN!  THE LAST KLINGON BATTLE"
3000 PRINT"CRUISER MENACING THE FEDERATION HAS BEEN DESTROYED."
     PRINT
3010 PRINT"YOUR EFFICIENCY RATING IS";1000*(K7/(T-T0))^2
     GOTO 2940
3020 FOR I=S1-1 TO S1+1
     FOR J=S2-1 TO S2+1
3030 IF INT(I+.5)<1 OR INT(I+.5)>8 THEN goto 3060
3040 IF INT(J+.5)<1 OR INT(J+.5)>8 THEN goto 3060
3050 a2$=">B<"
     Z1=I
	 Z2=J
	 GOSUB 4220
	 IF Z3=1 THEN goto 3070
3060 NEXT J
     NEXT I
	 D0=0
	 GOTO 3090
3070 D0=1
     c4$="DOCKED"
	 E=E0
	 P=P0
3080 PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES."
     S=0
	 GOTO 3110
3090 IF K3>0 THEN c4$="*RED*":GOTO 3110
3100 c4$="GREEN"
     IF E<E0*.1 THEN c4$="YELLOW"
3110 IF D(2)>=0 THEN goto 3130
3120 PRINT
     PRINT"*** SHORT RANGE SENSORS ARE OUT ***"
	 PRINT
	 RETURN
3130 O1$="   +--1---2---3---4---5---6---7---8-+"
     PRINT O1$
3140 FOR I=1 TO 8
     PRINT I;"|";
3150 FOR J=(I-1)*24+1 TO (I-1)*24+22 STEP 3
     PRINT" ";MID$(Q$,J,3);
3160 NEXT J
     PRINT" |";I
     NEXT I
     PRINT O1$
	 RETURN
3180 PRINT"STARDATE          ";INT(T*10)*.1
3200 PRINT"CONDITION         ";
     select case C4$
	     case "GREEN"
		     color rgb(GREEN)
		 case "*RED*"
		     COLOR rgb(BLACK),rgb(red)
		 case "YELLOW"
		     color rgb(yellow)
		 case "DOCKED"
		     color rgb(cyan)
	 end select
	 PRINT C4$;: color rgb(lightgray),rgb(black):print
3240 PRINT"QUADRANT          ";Q1;",";Q2
3260 PRINT"SECTOR            ";S1;",";S2
3280 PRINT"PHOTON TORPEDOES  ";:PRINT INT(P)
3300 PRINT"TOTAL ENERGY      ";:PRINT INT(E+S)
3320 PRINT"SHIELDS           ";:PRINT INT(S)
3340 PRINT"KLINGONS REMAINING";:PRINT INT(K9)
	 GOTO 970
3360 IF D(8)<0 THEN PRINT"COMPUTER DISABLED.":GOTO 970
3370 INPUT"COMPUTER ACTIVE AND AWAITING COMMAND";A:print
     IF A<0 THEN goto 970
3380 PRINT
     H8=1
	 ON A+1 GOTO 3480,3630,3740,4010,3800,3470
3390 PRINT"FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
3400 PRINT "-----------------------------------------"
     PRINT
3410 PRINT"   0 = CUMULATIVE GALTIC RECORD"
3420 PRINT"   1 = STATUS REPORT"
3430 PRINT"   2 = PHOTON TORPEDO DATA"
3440 PRINT"   3 = STARBASE NAV DATA"
3450 PRINT"   4 = DIRECTION/DISTANCE CALCULATOR"
3460 PRINT"   5 = GALAXY 'REGION NAME' MAP"
     PRINT
	 GOTO 3370
3470 H8=0
     G5=1
	 PRINT"                        THE GALAXY"
	 GOTO 3530
3480 REM
3490 REM
3500 PRINT
     PRINT"       ";
3510 PRINT"COMPUTER RECORD OF GALAXY FOR QUADRANT";Q1;",";Q2
3520 PRINT
3530 PRINT"       1     2     3     4     5     6     7     8"
3540 O1$="   +-----+-----+-----+-----+-----+-----+-----+-----+"
3550 PRINT O1$
     FOR I=1 TO 8
	 PRINT I;" ";
	 IF H8=0 THEN goto 3600
3560 FOR J=1 TO 8
     PRINT"| ";
	 IF Z(I,J)=0 THEN PRINT"*** ";:GOTO 3580
	 if (i=q1 and j=q2) then color rgb(gold)
3570 PRINT RIGHT$(STR$(Z(I,J)+1000),3);" ";
     color rgb(lightgray)
3580 IF J=8 THEN PRINT "|"
3590 NEXT J
     GOTO 3620
3600 Z4=I
     Z5=1
	 GOSUB 4250
	 J0=INT(15-.5*LEN(G2$))
	 PRINT TAB(J0);G2$;
3610 Z5=5
     GOSUB 4250
	 J0=INT(39-.5*LEN(G2$))
	 PRINT TAB(J0);G2$
3620 PRINT O1$
     NEXT I
	 PRINT
	 GOTO 970
3630 PRINT "   STATUS REPORT:"
     PRINT "   -------------"
	 x4$=""
3640 IF K9>1 THEN x4$="S"
3650 PRINT K9;" KLINGON";x4$;" LEFT."
3660 PRINT " MISSION MUST BE COMPLETED IN";.1*INT((T0+T9-T)*10);
3670 PRINT " STARDATES."
3680 IF B9<1 THEN goto 3720
3690 PRINT" THE FEDERATION IS MAINTAINING";B9
3700 PRINT" STARBASES IN THE GALAXY."
3710 GOTO 2590
3720 PRINT"YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN IN"
3730 PRINT"THE GALAXY -- YOU HAVE NO STARBASES LEFT!"
     GOTO 2590
3740 IF K3<=0 THEN goto 1950
3750 x4$=""
     IF K3>1 THEN x4$="S"
3760 PRINT"FROM ENTERPRISE TO KLINGON BATTLE CRUSER";x4$
3770 H8=0
     FOR I=1 TO 3:IF K(I,3)<=0 THEN goto 4000
3780 W1=K(I,1)
     X=K(I,2)
3790 C1=S1
     A=S2
	 GOTO 3850
3800 PRINT"DIRECTION/DISTANCE CALCULATOR:"
3810 PRINT"YOU ARE AT QUADRANT ";Q1;",";Q2;" SECTOR ";
3820 PRINT S1;",";S2;"."
3830 INPUT"PLEASE ENTER INITIAL COORDINATES (X,Y)";C1,A:print
3840 INPUT"FINAL COORDINATES (X,Y)";W1,X:print
3850 X=X-A
     A=C1-W1
	 IF X<0 THEN goto 3930
3860 IF A<0 THEN goto 3950
3870 IF X>0 THEN goto 3890
3880 IF A=0 THEN C1=5:GOTO 3900
3890 C1=1
3900 IF ABS(A)<=ABS(X)THEN goto 3920
3910 PRINT"DIRECTION =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A))
     GOTO 3990
3920 PRINT"DIRECTION =";C1+(ABS(A)/ABS(X))
     GOTO 3990
3930 IF A>0 THEN C1=3:GOTO 3960
3940 IF X<>0 THEN C1=5:GOTO 3900
3950 C1=7
3960 IF ABS(A)>=ABS(X)THEN goto 3980
3970 PRINT"DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X))
     GOTO 3990
3980 PRINT"DIRECTION =";C1+(ABS(X)/ABS(A))
3990 PRINT"DISTANCE =";SQR(X^2+A^2)
     IF H8=1 THEN goto 970
4000 NEXT I
     GOTO 970
4010 IF B3<>0 THEN PRINT"FROM ENTERPRISE TO STARBASE:"
4020 W1=B4
     X=B5
	 GOTO 3790
4030 PRINT"MR. SPOCK:  'SENSORS SHOW NO STARBASES IN THIS QUADRANT.'";
4040 GOTO 970
4050 R1=FNR(1)
     R2=FNR(1)
	 a2$="   "
	 Z1=R1
	 Z2=R2
	 GOSUB 4220
4060 IF Z3=0 THEN goto 4050
4070 RETURN
4080 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
4090 IF LEN(a2$)<>3 THEN PRINT"ERROR":end
4100 IF S8=1 THEN Q$=a2$+RIGHT$(Q$,189):RETURN
4110 IF S8=190 THEN Q$=LEFT$(Q$,189)+a2$:RETURN
4120 Q$=LEFT$(Q$,S8-1)+a2$+RIGHT$(Q$,190-S8):RETURN
4130 ON R1 GOTO 4140,4150,4160,4170,4180,4190,4200,4210
4140 G2$="WARP ENGINES":RETURN
4150 G2$="SHORT RANGE SENSORS":RETURN
4160 G2$="LONG RANGE SENSORS":RETURN
4170 G2$="PHASER CONTROL":RETURN
4180 G2$="PHOTON TUBES":RETURN
4190 G2$="DAMAGE CONTROL":RETURN
4200 G2$="SHIELD CONTROL":RETURN
4210 G2$="LIBRARY-COMPUTER":RETURN
4220 Z1=INT(Z1+.5)
     Z2=INT(Z2+.5)
	 S8=(Z2-1)*3+(Z1-1)*24+1
	 Z3=0
4230 IF MID$(Q$,S8,3)<>a2$ THEN RETURN
4240 Z3=1
     RETURN
4250 IF Z5<=4 THEN ON Z4 GOTO 4270,4280,4290,4300,4310,4320,4330,4340
4260 GOTO 4350
4270 G2$="ANTARES":GOTO 4440
4280 G2$="RIGEL":GOTO 4440
4290 G2$="PROCYON":GOTO 4440
4300 G2$="VEGA":GOTO 4440
4310 G2$="CANOPUS":GOTO 4440
4320 G2$="ALTAIR":GOTO 4440
4330 G2$="SAGITTARIUS":GOTO 4440
4340 G2$="POLLUX":GOTO 4440
4350 ON Z4 GOTO 4360,4370,4380,4390,4400,4410,4420,4430
4360 G2$="SIRIUS":GOTO 4440
4370 G2$="DENEB":GOTO 4440
4380 G2$="CAPELLA":GOTO 4440
4390 G2$="BETELGEUSE":GOTO 4440
4400 G2$="ALDEBARAN":GOTO 4440
4410 G2$="REGULUS":GOTO 4440
4420 G2$="ARCTURUS":GOTO 4440
4430 G2$="SPICA"
4440 IF G5<>1 THEN ON Z5 GOTO 4460,4470,4480,4490,4460,4470,4480,4490
4450 RETURN
4460 G2$=G2$+" I":RETURN
4470 G2$=G2$+" II":RETURN
4480 G2$=G2$+" III":RETURN
4490 G2$=G2$+" IV":RETURN
4500 END
